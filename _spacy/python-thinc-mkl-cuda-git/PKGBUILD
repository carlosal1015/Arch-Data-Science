# Maintainer: Mamy Ratsimbazafy <mamy (dot) ratsimbazafy_pkgbuild [at] gadz (dot) org>

pkgname=python-thinc-mkl-cuda-git
_pkgname=thinc
pkgver=v7.0.0.dev6.r0.gf26cb395
pkgrel=1
pkgdesc="spaCy\'s Machine Learning library for NLP in Python (CUDA accelerated)"
arch=('any')
url='https://github.com/explosion/thinc'
license=('MIT')
depends=('python-numpy' 'python-cupy' 'python-cymem' 'python-preshed' 'python-murmurhash' 'python-six' 'python-tqdm' 'python-cytoolz' 'python-plac' 'python-wrapt' 'python-dill' 'intel-mkl' 'python-msgpack' 'python-msgpack-numpy' 'python-pathlib')
makedepends=('git' 'python-setuptools' 'cython' 'intel-compiler-base')
conflicts=('python-thinc')
provides=('python-thinc')
source=('git://github.com/explosion/thinc.git')
md5sums=('SKIP' )

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}


build() {
  cd $_pkgname
  export CUDA_HOME=/opt/cuda
  export PATH=$PATH:$CUDA_HOME/bin
  # export CUDA9=1 # See https://github.com/explosion/spaCy/issues/1530 and 
  export THINC_BLAS=/opt/intel/mkl/lib/intel64/libmkl_rt.so

  source /opt/intel/mkl/bin/mklvars.sh intel64
  source /opt/intel/pkg_bin/compilervars.sh intel64

  python setup.py build
}

package() {
  cd $_pkgname
  python setup.py install --root=${pkgdir} --optimize=1
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/python-thinc
}
