# Maintainer: Mamy Ratsimbazafy <mamy (dot) ratsimbazafy_pkgbuild [at] gadz (dot) org>

pkgname=python-thinc-cuda-git
_pkgname=thinc
pkgver=v7.1.1.r2.gaefe67d4
pkgrel=1
pkgdesc="spaCy\'s Machine Learning library for NLP in Python (CUDA accelerated)"
arch=('any')
url='https://github.com/explosion/thinc'
license=('MIT')
depends=('python-numpy' 'python-cymem' 'python-preshed' 'python-murmurhash'
  'python-tqdm' 'python-plac' 'python-srsly' 'python-pathlib'
  'python-wasabi' 'python-cupy' 'python-thinc-gpu-ops'
  )
makedepends=('git' 'python-setuptools' 'cython' 'python-blis')
conflicts=('python-thinc')
provides=('python-thinc')
source=('git://github.com/explosion/thinc.git')
md5sums=('SKIP' )

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}


build() {
  cd $_pkgname
  python setup.py build
}

package() {
  cd $_pkgname
  python setup.py install --root=${pkgdir} --optimize=1
}
