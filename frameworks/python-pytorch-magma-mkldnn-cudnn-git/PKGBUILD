# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Stephen Zhang <zsrkmyn at gmail dot com>
# Update by Mamy Andr√©-Ratsimbazafy <Github mratsim>
# For Magma, MKLDNN, MKL, CUDNN, OpenCV (for video input)

pkgname=("python-pytorch-magma-mkldnn-cudnn-git")
_pkgname="pytorch"
pkgver=1.0rc0.r7206.gaa78523467
pkgrel=1
pkgdesc="Tensors and Dynamic neural networks in Python with strong GPU acceleration"
arch=('x86_64')
url="https://pytorch.org"
license=('BSD')
depends=('python' 'python-yaml' 'python-numpy' 'opencv' 'intel-mkl' 'intel-mpi'
         'cuda' 'cudnn' 'magma' 'nccl' 'cub' 'numactl' 'protobuf'
         #  'mkl-dnn-git' # PyTorch builds mkldnn statically
         'pybind11' # system pybind11 breaks sleef.h import (?) - https://github.com/pytorch/pytorch/issues/26555
         )
makedepends=('python-setuptools' 'cmake' 'git' 'intel-compiler-base' 'gcc8'
             'google-glog' 'gflags'
             )
provides=('python-pytorch' 'python-pytorch-cuda')
conflicts=('python-pytorch' 'python-pytorch-cuda')
source=("git+https://github.com/pytorch/pytorch.git")
sha256sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  # The Arch canonical way is buggy, version is stuck at 1.0 instead at 1.2.0
  # Hopefully nothing enforces pytorch 1.2 until bug is fixed:
  # https://github.com/pytorch/pytorch/issues/26258
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${_pkgname}"
  git submodule update --init --recursive
}

build() {
    cd "$srcdir/${_pkgname}"

  # sleef.h import issues - https://github.com/pytorch/pytorch/issues/26555
  # Suspects - combination of:
  #   - systemwide pybind11
  #   - systemwide mkldnn
  #   or
  #   - BUILD_CAFFE2_OPS=ON / MKLDNN_USE_NATIVE_ARCH=ON
  #   or
  #   - ATEN_NO_TEST=OFF
  #   or
  #   - USE_NATIVE_ARCH=ON (see https://github.com/pytorch/pytorch/issues/26403)

  # Delete the test as a workaround to all those issues
  msg2 "Ignoring aten/src/ATen/test/tensor_iterator_test.cpp to workaround https://github.com/pytorch/pytorch/issues/26555"
  sed -i 's#^  ${CMAKE_CURRENT_SOURCE_DIR}/tensor_iterator_test.cpp##g' aten/src/ATen/test/CMakeLists.txt

  export USE_GFLAGS=ON
  export USE_GLOG=ON

  # Caffe2 related
  export BUILD_CAFFE2_OPS=ON    # ON will trigger sleef import issue (?), ideep / MKLDNN will appear off, but will still be built statically
  export BUILD_CAFFE2_MOBILE=OFF

  # Tests
  export ATEN_NO_TEST=OFF        # OFF will trigger sleef import issue (?),
  # export BUILD_TEST=OFF        # Undefined symbol _ZN5torch3jit10testFusionEv - https://github.com/pytorch/pytorch/issues/26597

  # Vendor modules
  export BUILD_CUSTOM_PROTOBUF=OFF
  export USE_OPENCV=ON
  export USE_FFMPEG=ON
  export USE_MKLDNN=ON           # Requires BUILD_CAFFE2_OPS to appear ON in the summary. Still built.
  export USE_NNPACK=ON           # A bit redundant with MKLDNN hopefully PyTorch choose the best depending on op
  export USE_NUMPY=ON
  export USE_MAGMA=ON

  # CPU & GPU Arch
  export USE_NUMA=ON
  export USE_OPENCL=OFF
  export USE_ROCM=OFF
  export USE_OPENMP=ON
  export USE_TBB=OFF
  export USE_CUDA=ON
  export USE_CUDNN=ON
  export USE_NCCL=ON
  export USE_SYSTEM_NCCL=ON
  export USE_NATIVE_ARCH=ON         # ON will trigger sleef import issue (?),
  export MKLDNN_USE_NATIVE_ARCH=ON  # ON will trigger sleef import issue (?),

  export MKL_THREADING="OMP"
  export USE_MKLDNN_CBLAS=OFF       # Default to MKL if MKL is found

  # Compiling and linking
  export CC=gcc-8
  export CXX=g++-8
  export CUDAHOSTCXX=g++-8
  export CUDA_HOME=/opt/cuda
  export CUDNN_LIB_DIR=/usr/lib
  export CUDNN_INCLUDE_DIR=/usr/include
  export NCCL_INCLUDE_DIR=/usr/include
  export NCCL_LIB_DIR=/usr/lib
  export TORCH_CUDA_ARCH_LIST="7.5" # Consumer Turing
  export MAGMA_HOME=/opt/magma
  export OPENCV_INCLUDE_DIRS=/usr/include/opencv4
  export FFMPEG_INCLUDE_DIR=/usr/include # libavcodec, libavutils
  export FFMPEG_LIBRARIES=/usr/lib # libavcodec

  # # PyTorch builds mkldnn statically
  # export MKLDNN_LIBRARIES=/usr/lib
  # export MKLDNN_INCLUDE_DIR=/usr/include

  # For system CUB and Pybind11, otherwise PyTorch picks them from thirdparty submodules
  export CUB_INCLUDE_DIRS=/usr/include
  export pybind11_INCLUDE_DIR=/usr/include # will trigger sleef issue (?)

  # Link to Intel OpenMP like MKL-DNN
  # https://github.com/pytorch/pytorch/issues/12535
  # https://github.com/pytorch/pytorch/pull/15820
  # https://github.com/pytorch/pytorch/issues/19764
  export OpenMP_C_FLAGS="-liomp5"
  export OpenMP_CXX_FLAGS="-liomp5"
  export OpenMP_C_LIB_NAMES="iomp5"
  export OpenMP_CXX_LIB_NAMES="iomp5"
  export CMAKE_EXE_LINKER_FLAGS="-liomp5"
  export MKL_OPENMP_TYPE="Intel"
  export MKL_OPENMP_LIBRARY="/opt/intel/lib/libiomp5.so"

  export MKL_FIND_QUIETLY=OFF
  source /opt/intel/mkl/bin/mklvars.sh intel64
  source /opt/intel/pkg_bin/compilervars.sh intel64

  python setup.py build
}

package() {
  cd "$srcdir/${_pkgname}"
  # Prevent setup.py from re-running CMake and rebuilding
  sed -e 's/RUN_BUILD_DEPS = True/RUN_BUILD_DEPS = False/g' -i setup.py
  python setup.py install --root="$pkgdir"/ --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"

  msg2 "Pytorch has been build against Intel MPI, make sure to add '/opt/intel/composerxe/linux/mpi/intel64/libfabric/lib/' to /etc/ld.so.conf.d/intel-mpi.conf"
  msg2 "If OpenCV is built against Intel Parallel Studio, PyTorch will link against intel-tbb_psxe. The current one specifies a wrong path '/opt/intel/composerxe/tbb/lib/intel64/gcc4.7' in /etc/ld.so.conf.d/intel-tbb.conf, it should be either '/opt/intel/tbb/lib/intel64/libtbb/gcc4.7' or '/opt/intel/composerxe/linux/tbb/lib/intel64/libtbb/gcc4.7'"
}

# vim:set ts=2 sw=2 et:
