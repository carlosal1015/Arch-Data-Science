# Maintainer: pingplug <pingplug@foxmail.com>
# Contributor: cornholio <vigo.the.unholy.carpathian@gmail.com>
# Updated by Mamy Andr√©-Ratsimbazafy <Github mratsim
# for MKL support

##### Configuration Options
# Specify GPU compute capability Fermi (2.x) or Kepler (3.x) or Maxwell (5.x)
#_GPU_TARGET=Fermi
#_GPU_TARGET=Kepler
#_GPU_TARGET=Maxwell
#_GPU_TARGET=Pascal
#_GPU_TARGET=Volta
# Can also be one of these: sm_20 sm_30 sm_50 sm_60 sm_70 etc.
_GPU_TARGET=sm_61 # Consumer Pascal
##### End

pkgname=('magma-mkl')
_name=('magma')
provides=('magma')
conflicts=('magma')
pkgver=2.5.0
pkgrel=1
pkgdesc="Provides a dense linear algebra library similar to LAPACK but for heterogeneous/hybrid architectures, starting with current 'Multicore+GPU' systems (with CUDA)"
arch=('x86_64')
url="http://icl.cs.utk.edu/magma/"
license=('custom')
depends=("cuda>=6.5.0"
         'gsl'
         'python'
         'intel-mkl')
makedepends=('gcc-fortran'
             'intel-compiler-base'
             'cmake')
options=('staticlibs')
source=("http://icl.cs.utk.edu/projectsfiles/${_name}/downloads/${_name}-${pkgver}.tar.gz")
sha256sums=('4fd45c7e46bd9d9124253e7838bbfb9e6003c64c2c67ffcff02e6c36d2bcfa33')

build() {
  cd "${srcdir}/magma-${pkgver}"
  mkdir -p build-shared && pushd build-shared

  source /opt/intel/mkl/bin/mklvars.sh intel64
  source /opt/intel/pkg_bin/compilervars.sh intel64

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DGPU_TARGET=${_GPU_TARGET} \
    -DCUDA_HOST_COMPILER=/opt/cuda/bin/gcc \
    -DCMAKE_INSTALL_PREFIX=/opt/magma \
    ..
  if [[ -f "/usr/lib/ccache/bin/nvcc-ccache" ]] ; then
    cmake \
      -DCUDA_NVCC_EXECUTABLE=/usr/lib/ccache/bin/nvcc-ccache \
      -DCUDA_HOST_COMPILER=/usr/lib/ccache/bin/gcc-7 \
      ..
  fi
  make magma magma_sparse
  popd
  mkdir -p build-static && pushd build-static
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS:BOOL=OFF \
    -DGPU_TARGET=${_GPU_TARGET} \
    -DCUDA_HOST_COMPILER=/opt/cuda/bin/gcc \
    -DCMAKE_INSTALL_PREFIX=/opt/magma \
    ..
  if [[ -f "/usr/lib/ccache/bin/nvcc-ccache" ]] ; then
    cmake \
      -DCUDA_NVCC_EXECUTABLE=/usr/lib/ccache/bin/nvcc-ccache \
      -DCUDA_HOST_COMPILER=/usr/lib/ccache/bin/gcc-7 \
      ..
  fi
  make magma magma_sparse
  popd
}

package() {
  cd "${srcdir}/magma-${pkgver}/build-shared"
  # do not build test
  sed -i "s/install: preinstall/install: magma_sparse/g" Makefile
  make DESTDIR="${pkgdir}" install
  cd "${srcdir}/magma-${pkgver}/build-static"
  # do not build test
  sed -i "s/install: preinstall/install: magma_sparse/g" Makefile
  make DESTDIR="${pkgdir}" install

  mkdir -p ${pkgdir}/opt/magma/example
  cp -ru ${srcdir}/magma-${pkgver}/example/* ${pkgdir}/opt/magma/example/
  mkdir -p ${pkgdir}/opt/magma/testing
  cp -ru ${srcdir}/magma-${pkgver}/testing/* ${pkgdir}/opt/magma/testing/
  rm -rf ${pkgdir}/opt/magma/lib/pkgconfig
  mkdir -p ${pkgdir}/usr/share/licenses/magma
  cp ${srcdir}/magma-${pkgver}/COPYRIGHT ${pkgdir}/usr/share/licenses/magma/LICENSE
}

# vim:set ts=2 sw=2 et:
