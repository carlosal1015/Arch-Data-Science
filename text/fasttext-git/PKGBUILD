# Maintainer: Mamy Andr√©-Ratsimbazafy <Github: mratsim>

_name=fastText
pkgname=('fasttext-git' 'python-fasttext-git')
pkgver=v0.1.0.r82.ga5d22ab
pkgrel=1
url='https://github.com/facebookresearch/fastText'
license=('BSD')
source=('git+https://github.com/facebookresearch/fastText')
makedepends=('git' 'python-setuptools' 'python-numpy' 'python-scipy' 'python-pybind11')
arch=('x86_64')
sha256sums=('SKIP')

pkgver() {
  cd "${_name}"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare(){
  cp -r "${_name}" "${_name}-cpp"
  cp -r "${_name}" "${_name}-python"
}

build(){
  msg2 'Building C++ shared and static lib and standalone binary'
  cd "${srcdir}"/"${_name}-cpp"
  mkdir build && cd build 
  cmake .. -DCMAKE_INSTALL_PREFIX="${pkgdir}"/usr 
  make

  msg2 'Building Python bindings'
  cd "${srcdir}"/"${_name}-python"
  python setup.py build
}

package_fasttext-git() {
  pkgdesc='Library for fast text representation and classification.'
  provides=('fasttext')
  conflicts=('fasttext')

  cd "${_name}"-cpp/build
  make install 
}

package_python-fasttext-git() {
  pkgdesc='Library for fast text representation and classification. Python bindings'
  depends=('python'
           'python-pybind11'
           'python-numpy'
           'python-scipy')
  conflicts=('python-fasttext')
  provides=('python-fasttext')

  cd "${_name}"-python
  python setup.py install --root="${pkgdir}" --optimize=1
}

# vim:set ts=2 sw=2 et:
