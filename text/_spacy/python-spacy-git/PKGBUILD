# Maintainer: Mamy Ratsimbazafy <mamy (dot) ratsimbazafy_pkgbuild [at] gadz (dot) org>

pkgname=python-spacy-git
_pkgname=spacy
pkgver=2.1.8.r355.gf2d224756
pkgrel=1
pkgdesc='Industrial-strength Natural Language Processing (NLP) with Python and Cython'
arch=('any')
url='https://github.com/explosion/spaCy'
license=('MIT')
depends=('python-numpy' 'python-cymem' 'python-preshed' 'python-thinc' 'python-blis'
          'python-murmurhash' 'python-srsly' 'python-plac'
          'python-requests')
makedepends=('git' 'python-setuptools' 'cython' 'python-jsonschema')
optdepends=('python-jsonschema')
conflicts=('python-spacy')
provides=('python-spacy')
source=('git+https://github.com/explosion/spacy#branch=develop')
md5sums=('SKIP' )

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}


build() {
  cd $_pkgname
  python setup.py build
}

package() {
  cd $_pkgname
  python setup.py install --root=${pkgdir} --optimize=1
}
