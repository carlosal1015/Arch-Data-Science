# Maintainer: Mamy Andr√©-Ratsimbazafy <Github: mratsim>

_name=sentencepiece
pkgname=('python-sentencepiece-git')
pkgver=0.1.83.r7.g60e42a7
pkgrel=1
pkgdesc='Unsupervised text tokenizer for Neural Network-based text generation. Python bindings'
url='https://github.com/google/sentencepiece'
license=('Apache')
source=('git+https://github.com/google/sentencepiece')
depends=('sentencepiece')
makedepends=('git' 'python-setuptools')
provides=('python-sentencepiece')
conflicts=('python-sentencepiece')
arch=('x86_64')
sha256sums=('SKIP')

pkgver() {
  cd "${_name}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  msg2 'Building Python bindings'
  cd "${_name}"/python
  python setup.py build
}


package() {
  cd "${_name}"/python
  python setup.py install --root="${pkgdir}" --optimize=1
}

# vim:set ts=2 sw=2 et:
